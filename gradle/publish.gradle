/*
 * Copyright 2023 Squircle CE contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.blacksquircle.gradle.Gradle

apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    if (!project.plugins.findPlugin("com.android.library")) {
        from sourceSets.main.allJava
    }
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

// Default values
ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties props = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is -> props.load(is) }
    props.each { name, value -> ext[name] = value }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
}

publishing {
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
    publications {
        release(MavenPublication) {
            groupId Gradle.Maven.libraryGroupId
            artifactId Gradle.Maven.libraryArtifactId
            version Gradle.Maven.libraryVersionName

            if (project.plugins.findPlugin("com.android.library")) {
                artifact("$buildDir/outputs/aar/${Gradle.Maven.libraryArtifactId}-release.aar")
            } else {
                artifact("$buildDir/libs/${Gradle.Maven.libraryArtifactId}-${version}.jar")
            }
            artifact androidSourcesJar
            artifact javadocJar

            pom {
                name = Gradle.Maven.libraryArtifactId
                description = 'EditorKit is a multi-language code editor for Android.'
                url = 'https://github.com/massivemadness/EditorKit'
                licenses {
                    license {
                        name = 'Apache 2.0 License'
                        url = 'https://github.com/massivemadness/EditorKit/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'massivemadness'
                        name = 'Dmitrii Rubtsov'
                        email = 'dm.mironov01@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/massivemadness/EditorKit.git'
                    developerConnection = 'scm:git:ssh://github.com/massivemadness/EditorKit.git'
                    url = 'https://github.com/massivemadness/EditorKit/tree/master'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.dependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', "runtime")
                    }
                    configurations.api.dependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', "compile")
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

afterEvaluate {
    if (project.plugins.findPlugin("com.android.library")) {
        signReleasePublication.dependsOn bundleReleaseAar // Android module
    } else {
        signReleasePublication.dependsOn jar // Java module
    }
    publishReleasePublicationToSonatypeRepository.dependsOn build
}

// ./gradlew publishReleasePublicationToSonatypeRepository --no-parallel