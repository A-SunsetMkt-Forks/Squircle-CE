/*
 * Copyright 2021 Brackeys IDE contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (!project.rootProject.file('local.properties').exists()) {
    logger.warn("local.properties not found.")
    return
}

apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact androidJavadocsJar
                artifact androidSourcesJar

                groupId = libraryGroupId
                artifactId = libraryArtifactId
                version = versions.publishVersionName

                configurations.api.allDependencies.each { dep ->
                    if (dep.getGroup() == libraryGroupId && dep.getVersion() == "unspecified") {
                        dep.setVersion(versions.publishVersionName)
                        println("Updated $dep")
                    }
                }
            }
        }
    }
}

def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['release']
    dryRun = false
    publish = true
    override = false
    pkg {
        repo = 'Brackeys-IDE'
        name = libraryArtifactId
        desc = 'Brackeys IDE is a fast and free multi-language code editor for Android.'
        userOrg = 'massivemadness'
        licenses = ['Apache-2.0']
        githubRepo = 'massivemadness/Brackeys-IDE'
        githubReleaseNotesFile = 'README.md'
        websiteUrl = 'https://github.com/massivemadness/Brackeys-IDE'
        issueTrackerUrl = 'https://github.com/massivemadness/Brackeys-IDE/issues'
        vcsUrl = 'https://github.com/massivemadness/Brackeys-IDE.git'
        version {
            name = versions.publishVersionName
            released = new Date()
        }
    }
}