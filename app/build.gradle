/*
 * Licensed to the Light Team Software (Light Team) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The Light Team licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    def buildConfig = rootProject.extensions.getByName("ext")

    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools
    defaultConfig {
        applicationId "com.KillerBLS.modpeide"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode 10033
        versionName "2020.2.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    flavorDimensions 'version'
    productFlavors {
        standard {
            dimension 'version'
            applicationId "com.KillerBLS.modpeide"
        }
        ultimate {
            dimension 'version'
            applicationId "com.LightTeam.modpeidepro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            res.srcDirs = [
                    file("src/main/res/layouts/").listFiles(),
                    'src/main/res/layouts',
                    'src/main/res'
            ]
        }
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    dataBinding {
        enabled true
    }
    viewBinding {
        enabled true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Kotlin
    implementation library.kotlin
    implementation library.core_ktx

    //Design
    implementation library.appcompat
    implementation library.materialdesign
    implementation library.constraintlayout
    implementation library.swiperefreshlayout
    implementation library.preferencescompat
    implementation library.materialdialogs_core
    implementation library.materialdialogs_color
    implementation library.recyclerview_selection

    //AAC
    implementation library.navigation_ui
    implementation library.navigation_fragment
    implementation library.livedata
    implementation library.viewmodel

    //Network
    implementation library.playcore
    implementation library.gson

    //Rx
    implementation library.rxjava2
    implementation library.rxandroid
    implementation library.rxkotlin
    implementation library.rxbinding_appcompat
    implementation library.rxpreferences

    //DI
    implementation library.dagger2
    kapt library.dagger2_compiler
    implementation library.dagger2_android
    kapt library.dagger2_android_processor

    //Other
    implementation library.keyboardevent

    //Modules
    implementation project(':domain')
    implementation project(':data')
    implementation project(':database')
    implementation project(':editorkit')
    implementation project(':filesystems:filesystem')
    implementation project(':filesystems:localfilesystem')
    implementation project(':languages:language')

    //Tests
    testImplementation testLibrary.junit
    testImplementation testLibrary.kunit

    androidTestImplementation androidTestLibrary.junit_ext
    androidTestImplementation androidTestLibrary.test_runner
    androidTestImplementation androidTestLibrary.espresso_core
}
